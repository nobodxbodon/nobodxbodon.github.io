<h2><a href="https://github.com/caol64/caol64.github.io/blob/master/content/posts/2023/2023-12-20-ps2mc-browsers-shader-introduction.en.md">仓库源文</a>，<a href="https://babyno.top/posts/2023/12/20/ps2mc-browsers-shader-introduction.en">站点原文</a></h2>
<hr/>
<p>author: caol64
title: '"ps2mc-browser" Shader Code Analysis'
slug: ps2mc-browsers-shader-introduction
description: Uncover the magic behind colorful 3D rendering using OpenGL Shader with the ps2mc-browser. Dive deep into how polygons, vertices, and textures combine to create vibrant tabletop graphics.
date: 2023-12-20 12:07:17
draft: false
ShowToc: true
TocOpen: true
tags:</p>
<ul>
<li>ps2mc-browser</li>
<li>OpenGL</li>
<li>Python
categories:</li>
<li>Tutorial</li>
</ul>
<hr/>
<p>How do we render the vertices and textures of polygons into a colorful scene? This is where OpenGL shaders come into play. Today, we'll discuss the shaders of <code>ps2mc-browser</code>.</p>
<p>To briefly introduce, <a href="https://github.com/caol64/ps2mc-browser">ps2mc-browser</a> is a PS2 memory card viewer capable of parsing vertex and texture data from 3D icons within PS2 memory card files and rendering them using OpenGL.</p>
<p>In the following content, I'll dissect the six OpenGL shaders used in <code>ps2mc-browser</code> one by one. Let's dive into understanding how they work.</p>
<h2>Background Shaders (bg.frag and bg.vert)</h2>
<p>These shaders are primarily responsible for rendering the background color. As we learned from previous articles, the <code>icon.sys</code> file provides data for the colors and transparency of the four vertices of the background.</p>
<p>Let's revisit the coordinate system we created:</p>
<p><img alt="" src="/media/wwww/share/study/聚聚/源数据/博客聚合/babyno.top/content/imgs/posts/2023-10-09-rendering-ps2-3d-icon/%E5%AD%98%E5%82%A8%E5%8D%A1-%E5%9D%90%E6%A0%87%E7%B3%BB.jpg"/></p>
<p>Since the space we create is a cube with each side having a length of 2, and the origin coordinates are at the center of the cube. Additionally, our camera is in the negative direction of the <code>z-axis</code>. Let's imagine which face of the cube should be the background face:</p>
<p><img alt="" src="/media/wwww/share/study/聚聚/源数据/博客聚合/babyno.top/content/imgs/posts/2023-12-20-ps2mc-browsers-shader-introduction/image.webp"/></p>
<p>Therefore, we can construct the coordinates of the four vertices of the background as:</p>
<pre><code class="language-python">bg_vertex = [(-1, 1, 0.99), (-1, -1, 0.99), (1, -1, 0.99), (1, 1, 0.99)]
</code></pre>
<p>By the way, in shaders, rendering is done on a per-triangle basis. Therefore, this square face should be split into two triangles. Now, we'll fill in the corresponding color values for these 4 coordinates, and the shader will render the color for the entire face.</p>
<pre><code class="language-glsl">// bg.frag
#version 330 core
in vec4 fragColor0;
out vec4 fragColor;
void main() {
    fragColor = fragColor0;
}
</code></pre>
<p>In the fragment shader for the background (<code>bg.frag</code>), we input a color (<code>fragColor0</code>), and then assign its value to the output color (<code>fragColor</code>). This process essentially renders the background color.</p>
<pre><code class="language-glsl">// bg.vert
#version 330 core
in vec3 vertexPos;
in vec4 vertexColor;
out vec4 fragColor0;
void main() {
    fragColor0 = vertexColor;
    gl_Position = vec4(vertexPos, 1.0);
}
</code></pre>
<p>In the vertex shader for the background (<code>bg.vert</code>), we first define a vertex position (<code>vertexPos</code>) and a color value (<code>vertexColor</code>) as inputs. Then, we pass the color value to the fragment shader and set the vertex position (<code>gl_Position</code>).</p>
<p>It can be said that the main responsibility of the background shader is to fill the desired color onto the screen.</p>
<p>We're not done yet. Previously, we mentioned that besides color data, the background also has transparency data. How do we represent transparency? We'll add another layer behind the background layer, which we call the <code>skybox</code> layer, and add some color to this layer. This way, if the background layer has transparency, some of the color from the skybox layer will show through the background layer, achieving the desired visual effect.</p>
<pre><code class="language-python">skybox_vertex = [(-1, 1, 0.999), (-1, -1, 0.999), (1, -1, 0.999), (1, 1, 0.999)]
skybox_colors = [
    (0.6, 0.6, 0.6, 1),
    (0.6, 0.6, 0.6, 1),
    (0.6, 0.6, 0.6, 1),
    (0.6, 0.6, 0.6, 1),
]
</code></pre>
<p>Finally, here's an example of the background effect. You can see that the color transition in the middle is automatically calculated by shader interpolation.</p>
<p><img alt="" src="/media/wwww/share/study/聚聚/源数据/博客聚合/babyno.top/content/imgs/posts/2023-12-20-ps2mc-browsers-shader-introduction/%E6%88%AA%E5%B1%8F2023-12-20%2014.41.10.webp"/></p>
<h2>Icon Shaders (icon.frag and icon.vert)</h2>
<p>This is the most complex part, responsible for rendering the 3D icons parsed from the PS2 memory card files. As mentioned earlier, shaders render triangles formed by vertices one by one.</p>
<pre><code class="language-glsl">// icon.frag
#version 330 core
...
// Uniform variables
uniform sampler2D texture0;  // Texture
uniform vec4 ambient;        // Ambient light
uniform mat4 model;          // Model matrix
uniform Light lights[MAX_NUM_TOTAL_LIGHTS];  // Array of lights
void main() {
    // Calculate normalized normal vector
    vec3 normal = normalize(normal0).xyz;
    // Get color from the texture
    vec3 color = texture(texture0, uv0).rgb;
    // Calculate diffuse lighting
    vec3 diffuse = vec3(0);
    for (int i = 0; i &lt; MAX_NUM_TOTAL_LIGHTS; i++) {
        vec3 lightDir = normalize(lights[i].dir.xyz);
        diffuse += max(dot(normal, lightDir), 0.0) * lights[i].color.rgb;
    }
    // Final color calculation
    vec4 finalColor = vec4((ambient.rgb + diffuse) * color, 1.0);
    fragColor = finalColor;
}
</code></pre>
<p>In the fragment shader for the icons (<code>icon.frag</code>), the "uniform" keyword introduces some constants, including the texture (<code>texture0</code>), ambient light (<code>ambient</code>), and lighting (<code>lights</code>).</p>
<p>Next, we calculate a unit normal vector (<code>normal</code>), retrieve the color from the texture, and compute the diffuse reflection (<code>diffuse</code>). Specifically, each light contributes to the diffuse reflection, which depends on the angle between the light direction and the normal vector. We sum up the contributions of all lights to obtain the final diffuse reflection.</p>
<p>Finally, we multiply the ambient light and diffuse reflection by the texture color to get the final color (<code>finalColor</code>), which is then passed to the output color (<code>fragColor</code>) of the fragment shader.</p>
<pre><code class="language-glsl">// icon.vert
#version 330 core
...
// Output variables for fragment shader
out vec2 uv0;            // Texture coordinates for fragment shader
out vec4 normal0;        // Transformed normal for fragment shader
// Uniform matrices
uniform mat4 proj;       // Projection matrix
uniform mat4 view;       // View matrix
uniform mat4 model;      // Model matrix
uniform float tweenFactor; // Tweening factor for vertex animation
void main() {
    // Pass texture coordinates to fragment shader
    uv0 = texCoord;
    // Transform and pass normal to fragment shader
    normal0 = model * vec4(normal, 1);
    // Interpolate between current and next vertex positions based on tween factor
    vec4 basePos = vec4(mix(vertexPos, nextVertexPos, tweenFactor), 1.0);
    // Combine transformations and set the final vertex position
    gl_Position = proj * view * model * basePos;
}
</code></pre>
<p>In the vertex shader for the icons (<code>icon.vert</code>), we use the <code>uniform</code> keyword to set up some matrices (<code>proj</code>, <code>view</code>, <code>model</code>) and an interpolation factor (<code>tweenFactor</code>).</p>
<p>Let's focus on the interpolation factor (<code>tweenFactor</code>) here. This value is crucial for achieving animation effects in 3D icons. Since each memory card contains not only the complete set of vertices for the icon but also additional vertex coordinates for different actions, which we refer to as action frames, animating the frames in a loop creates the animation effect. Therefore, we need to interpolate between adjacent action frames to calculate the vertex coordinates. The <code>tweenFactor</code> represents the time factor between the current frame and the next frame.</p>
<p>Next, we pass the texture coordinates (<code>uv0</code>) and the normal vector (<code>normal0</code>) to the fragment shader. We interpolate between the current vertex position (<code>vertexPos</code>) and the next vertex position (<code>nextVertexPos</code>) based on the tween factor. Then, we transform the interpolated position using the model matrix (<code>model</code>) and finally transform it into homogeneous clipping space using the view matrix (<code>view</code>) and projection matrix (<code>proj</code>).</p>
<p>With this, the icon shader's task is completed.</p>
<h2>Button Shaders (circle.frag and circle.vert)</h2>
<p>Responsible for rendering interactive buttons for mouse interaction. <code>ps2mc-browser</code> can display various actions of characters in the memory card. Users can click on the corresponding buttons to switch between different actions.</p>
<pre><code class="language-glsl">// circle.frag
#version 330 core
out vec4 fragColor;
void main() {
    fragColor = vec4(1.0, 1.0, 1.0, 0.6);
}
</code></pre>
<p>In this <code>circle.frag</code>, we directly set the color (<code>fragColor</code>) to semi-transparent white and do not accept any inputs.</p>
<pre><code class="language-glsl">// circle.vert
#version 330 core
in vec2 vertexPos;
void main() {
    gl_Position = vec4(vertexPos, 0, 1.0);
}
</code></pre>
<p>In <code>circle.vert</code>, we only need to receive a vertex position (<code>vertexPos</code>) as input and then assign it to the OpenGL built-in variable <code>gl_Position</code>.</p>
<p>In simple terms, the button shader's job is to draw a semi-transparent white geometric shape as a button. The number of buttons it needs to render and their coordinates are all calculated by the program and passed to the shader.</p>
<h2>Summary</h2>
<p>So far, we have detailed how <code>ps2mc-browser</code> renders 3D dynamic icons using OpenGL shaders. Both Python and OpenGL were new to me, and I didn't expect to integrate them together for this project. There may be some new features added to this project in the future, so until next time, goodbye.</p>
