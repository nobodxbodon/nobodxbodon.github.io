<h2><a href="https://gitee.com/jaredtao/jaredtao/blob/master/source/_posts/Qml/玩转Qml(16">仓库源文</a>-移植ShaderToy.md)，<a href="https://jaredtao.gitee.io/玩转Qml(16">站点原文</a>-移植ShaderToy)</h2>
<hr/>
<p>title: 玩转Qml(16)-移植ShaderToy
photos: /img/avatar.jpg
tags:</p>
<ul>
<li>Qt</li>
<li>Qml</li>
<li>QtQuick</li>
<li>特效
categories: 玩转Qml
abbrlink: 9836
date: 2019-07-04 13:44:23</li>
</ul>
<hr/>
<ul>
<li><a href="#简介">简介</a></li>
<li><a href="#源码">源码</a></li>
<li><a href="#效果预览">效果预览</a><ul>
<li><a href="#穿云洞">穿云洞</a></li>
<li><a href="#星球之光">星球之光</a></li>
<li><a href="#蜗牛">蜗牛</a></li>
<li><a href="#超级马里奥">超级马里奥</a></li>
</ul>
</li>
<li><a href="#关于shadertoy">关于ShaderToy</a></li>
<li><a href="#关于shadereffect">关于ShaderEffect</a></li>
<li><a href="#shadertoy原理">ShaderToy原理</a><ul>
<li><a href="#约定的变量">约定的变量</a></li>
<li><a href="#glsl版本号">glsl版本号</a></li>
<li><a href="#glsl版本兼容">glsl版本兼容</a></li>
<li><a href="#shadertoy适配">ShaderToy适配</a></li>
</ul>
</li>
<li><a href="#taoshadertoy">TaoShaderToy</a></li>
</ul>
<h2>简介</h2>
<p>这次涛哥将会教大家移植ShaderToy的特效到Qml</p>
<h2>源码</h2>
<p>《玩转Qml》系列文章，配套了一个优秀的开源项目:TaoQuick</p>
<p>github <a href="https://github.com/jaredtao/TaoQuick">https://github.com/jaredtao/TaoQuick</a></p>
<p>访问不了或者速度太慢，可以用国内的镜像网站gitee</p>
<p><a href="https://gitee.com/jaredtao/TaoQuick">https://gitee.com/jaredtao/TaoQuick</a></p>
<h2>效果预览</h2>
<p>先看几个效果图</p>
<h3>穿云洞</h3>
<p><img alt="" src="/Users/xuanwu/work/聚聚/中文博客集锦/源数据/博客聚合/jaredtao.gitee.io/source/images/ShaderToy/Preview1.gif"/></p>
<h3>星球之光</h3>
<p><img alt="" src="/Users/xuanwu/work/聚聚/中文博客集锦/源数据/博客聚合/jaredtao.gitee.io/source/images/ShaderToy/Preview2.gif"/></p>
<h3>蜗牛</h3>
<p><img alt="" src="/Users/xuanwu/work/聚聚/中文博客集锦/源数据/博客聚合/jaredtao.gitee.io/source/images/ShaderToy/Preview3.gif"/></p>
<h3>超级马里奥</h3>
<p><img alt="" src="/Users/xuanwu/work/聚聚/中文博客集锦/源数据/博客聚合/jaredtao.gitee.io/source/images/ShaderToy/Preview4.gif"/></p>
<p>gif录制质量较低，可编译运行TaoQuick源码或使用涛哥打包好的可执行程序，查看实际运行效果。</p>
<p>可执行程序下载链接(包括windows 和 MacOS平台) <a href="https://github.com/jaredtao/TaoQuick/releases">https://github.com/jaredtao/TaoQuick/releases</a></p>
<h2>关于ShaderToy</h2>
<p>学习过计算机图形学的人，都应该知道大名鼎鼎的ShaderToy网站</p>
<p>用一些Shader代码和简单的纹理，就可以输出各种酷炫的图形效果和音频效果。</p>
<p>如果你还不知道，赶紧去看看吧<a href="https://www.shadertoy.com">https://www.shadertoy.com</a></p>
<p>顺便提一下，该网站的作者是IQ大神，这里有他的博客：</p>
<p><a href="http://www.iquilezles.org/www/articles/raymarchingdf/raymarchingdf.htm">http://www.iquilezles.org/www/articles/raymarchingdf/raymarchingdf.htm</a></p>
<p>本文主要讨论图形效果，音频效果以后再实现。</p>
<h2>关于ShaderEffect</h2>
<p>Qml中实现ShaderToy，最快的途径就是ShaderEffect了。</p>
<p>上一篇文章《Qml特效-着色器效果ShaderEffect》已经介绍过ShaderEffect了, 本文重点是移植ShaderToy。</p>
<p>在涛哥写这篇文章之前，已经有两位前辈做过相关的研究。</p>
<p>陈锦明： <a href="https://zhuanlan.zhihu.com/p/38942460">https://zhuanlan.zhihu.com/p/38942460</a></p>
<p>qyvlik: <a href="https://zhuanlan.zhihu.com/p/44417680">https://zhuanlan.zhihu.com/p/44417680</a></p>
<p>涛哥参考了他们的实现，做了一些改进、完善。</p>
<p>在此感谢两位前辈。</p>
<p>下面正文开始</p>
<h2>ShaderToy原理</h2>
<p>OpenGL的可编程渲染管线中，着色器代码是可以动态编译、加载到GPU运行的。</p>
<p>而OpenGL又包括了桌面版(OpenGL Desktop)、嵌入式版(OpenGL ES)以及网页版(WebGL)</p>
<p>ShaderToy网站是以WebGL 2.0为基础，提供内置函数、变量，并约定了一些输入变量，由用户按照约定编写着色器代码。</p>
<p>只要不是太老的OpenGL版本，内置函数、变量基本都是通用的。</p>
<h3>约定的变量</h3>
<p>ShaderToy网站约定的变量如下：</p>
<pre><code class="lang-glsl">vec3        iResolution             image/buffer        The viewport resolution (z is pixel aspect ratio, usually 1.0)
float       iTime                   image/sound/buffer  Current time in seconds
float       iTimeDelta              image/buffer        Time it takes to render a frame, in seconds
int         iFrame                  image/buffer        Current frame
float       iFrameRate              image/buffer        Number of frames rendered per second
float       iChannelTime[4]         image/buffer        Time for channel (if video or sound), in seconds
vec3        iChannelResolution[4]   image/buffer/sound  Input texture resolution for each channel
vec4        iMouse                  image/buffer        xy = current pixel coords (if LMB is down). zw = click pixel
sampler2D   iChannel{i}             image/buffer/sound  Sampler for input textures i
vec4        iDate                   image/buffer/sound  Year, month, day, time in seconds in .xyzw
float       iSampleRate             image/buffer/sound  The sound sample rate (typically 44100)
</code></pre>
<p>Qml中的相应实现</p>
<pre><code class="lang-qml">ShaderEffect {
    id: shader

    //properties for shader

    //not pass to shader
    readonly property vector3d defaultResolution: Qt.vector3d(shader.width, shader.height, shader.width / shader.height)
    function calcResolution(channel) {
        if (channel) {
            return Qt.vector3d(channel.width, channel.height, channel.width / channel.height);
        } else {
            return defaultResolution;
        }
    }
    //pass
    readonly property vector3d  iResolution: defaultResolution
    property real       iTime: 0
    property real       iTimeDelta: 100
    property int        iFrame: 10
    property real       iFrameRate
    property vector4d   iMouse;
    property var iChannel0; //only Image or ShaderEffectSource
    property var iChannel1; //only Image or ShaderEffectSource
    property var iChannel2; //only Image or ShaderEffectSource
    property var iChannel3; //only Image or ShaderEffectSource
    property var        iChannelTime: [0, 1, 2, 3]
    property var        iChannelResolution: [calcResolution(iChannel0), calcResolution(iChannel1), calcResolution(iChannel2), calcResolution(iChannel3)]
    property vector4d   iDate;
    property real       iSampleRate: 44100

    ...

}
</code></pre>
<p>其中时间、日期通过Timer刷新，鼠标位置用MouseArea刷新。</p>
<p>同时涛哥导出了hoverEnabled、running属性和restart函数，以方便Qml中控制Shader的运行。</p>
<pre><code class="lang-qml">ShaderEffect {
    id: shader
...
    //properties for Qml controller
    property alias hoverEnabled: mouse.hoverEnabled
    property bool running: true
    function restart() {
        shader.iTime = 0
        running = true
        timer1.restart()
    }

    Timer {
        id: timer1
        running: shader.running
        triggeredOnStart: true
        interval: 16
        repeat: true
        onTriggered: {
            shader.iTime += 0.016;
        }
    }
    Timer {
        running: shader.running
        interval: 1000
        onTriggered: {
            var date = new Date();
            shader.iDate.x = date.getFullYear();
            shader.iDate.y = date.getMonth();
            shader.iDate.z = date.getDay();
            shader.iDate.w = date.getSeconds()
        }
    }
    MouseArea {
        id: mouse
        anchors.fill: parent
        onPositionChanged: {
            shader.iMouse.x = mouseX
            shader.iMouse.y = mouseY
        }
        onClicked: {
            shader.iMouse.z = mouseX
            shader.iMouse.w = mouseY
        }
    }
...
}
</code></pre>
<h3>glsl版本号</h3>
<p>GLSL Versions</p>
<table>
<thead><tr>
<th>OpenGL Version</th>
<th>GLSL Version</th>
</tr>
</thead>
<tbody>
<tr>
<td>2.0</td>
<td>110</td>
</tr>
<tr>
<td>2.1</td>
<td>120</td>
</tr>
<tr>
<td>3.0</td>
<td>130</td>
</tr>
<tr>
<td>3.1</td>
<td>140</td>
</tr>
<tr>
<td>3.2</td>
<td>150</td>
</tr>
<tr>
<td>3.3</td>
<td>330</td>
</tr>
<tr>
<td>4.0</td>
<td>400</td>
</tr>
<tr>
<td>4.1</td>
<td>410</td>
</tr>
<tr>
<td>4.2</td>
<td>420</td>
</tr>
<tr>
<td>4.3</td>
<td>430</td>
</tr>
</tbody>
</table>
<p>GLSL ES Versions (Android, iOS, WebGL)</p>
<table>
<thead><tr>
<th>OpenGL ES Version</th>
<th>GLSL ES Version</th>
</tr>
</thead>
<tbody>
<tr>
<td>2.0</td>
<td>100</td>
</tr>
<tr>
<td>3.0</td>
<td>300</td>
</tr>
</tbody>
</table>
<h3>glsl版本兼容</h3>
<p>ShaderToy限定了WebGL 2.0，而我们移植到Qml中，自然是希望能够在所有可以运行Qml的设备上运行ShaderToy效果。</p>
<p>所以要做一些glsl版本相关的处理。</p>
<p>涛哥研究了Qt的GraphicsEffects模块源码，它的版本处理要么默认，要么 150 core，显然是不够用的。</p>
<p>glsl各个版本的差异，可以参考这里 <a href="https://github.com/mattdesl/lwjgl-basics/wiki/glsl-versions">https://github.com/mattdesl/lwjgl-basics/wiki/glsl-versions</a></p>
<p>涛哥总结出了如下的代码和注释说明：</p>
<p>注意"#version  xxx"必须是着色器的第一行，不能换行</p>
<pre><code class="lang-qml">
    // 如果环境是OpenGL ES2，默认的version是 version 110, 不需要写出来。
    // 比ES2更老的版本是ES 1.0 和 ES 1.1, 这种古董设备，建议还是不要玩Shader了吧。
    // ES2没有texture函数，要用旧的texture2D代替
    // 精度限定要写成float

    readonly property string gles2Ver: "
#define texture texture2D
precision mediump float;
"
    // 如果环境是OpenGL ES3，version是 version 300 es
    // ES 3.1 ES 3.2也可以。
    // ES3可以用in out 关键字，gl_FragColor也可以用out fragColor取代
    // 精度限定要写成float

    readonly property string gles3Ver: "#version 300 es
#define varying in
#define gl_FragColor fragColor
precision mediump float;

out vec4 fragColor;
"
    // 如果环境是OpenGL Desktop 3.x，version这里参考Qt默认的version 150。大部分Desktop设备应该
    // 都是150, 即3.2版本，第一个区分Core和Compatibility的版本。
    // Core是核心模式，只有核心api以减轻负担。相应的Compatibility是兼容模式，保留全部API以兼容低版本。
    // Desktop 3.x 可以用in out 关键字，gl_FragColor也可以用out fragColor取代
    // 精度限定抹掉，用默认的。不抹掉有些情况下会报错，不能通用。
    readonly property string gl3Ver: "#version 150
#define varying in
#define gl_FragColor fragColor
#define lowp
#define mediump
#define highp

out vec4 fragColor;
"
    // 如果环境是OpenGL Desktop 2.x，version这里就用2.0的version 110，即2.0版本
    // 2.x 没有texture函数，要用旧的texture2D代替
    readonly property string gl2Ver: "#version 110
#define texture texture2D
"
    property string versionString: {
        if (Qt.platform.os === "android") {
            if (GraphicsInfo.majorVersion === 3) {
                console.log("android gles 3")
                return gles3Ver
            } else {
                console.log("android gles 2")
                return gles2Ver
            }
        } else {
            if (GraphicsInfo.majorVersion === 3 ||GraphicsInfo.majorVersion === 4) {
                return gl3Ver
            } else {
                return gl2Ver
            }
        }
    }
    readonly property string forwardString: versionString + "
        varying vec2 qt_TexCoord0;
        varying vec4 vertex;
        uniform lowp   float qt_Opacity;

        uniform vec3   iResolution;
        uniform float  iTime;
        uniform float  iTimeDelta;
        uniform int    iFrame;
        uniform float  iFrameRate;
        uniform float  iChannelTime[4];
        uniform vec3   iChannelResolution[4];
        uniform vec4   iMouse;
        uniform vec4    iDate;
        uniform float   iSampleRate;
        uniform sampler2D   iChannel0;
        uniform sampler2D   iChannel1;
        uniform sampler2D   iChannel2;
        uniform sampler2D   iChannel3;
    "
</code></pre>
<p>versionString 这里，主要测试了Desktop和 android设备，Desktop只要显卡不太搓，都能运行的。</p>
<p>Android ES3的也是全部支持，ES2的部分不能运行，比如iq大神的蜗牛Shader，使用了textureLod等一系列内置函数，就不能在ES2上面跑。</p>
<h3>ShaderToy适配</h3>
<p>本来是不需要写顶点着色器的。如果我们想把ShaderToy做成一个任意坐标开始的Item来用，就需要适配一下坐标。</p>
<p>涛哥写的顶点着色器如下，仅在默认着色器的基础上，传递qt_Vertex给下一阶段的vertex</p>
<pre><code class="lang-qml">
    vertexShader: "
              uniform mat4 qt_Matrix;
              attribute vec4 qt_Vertex;
              attribute vec2 qt_MultiTexCoord0;
              varying vec2 qt_TexCoord0;
              varying vec4 vertex;
              void main() {
                  vertex = qt_Vertex;
                  gl_Position = qt_Matrix * vertex;
                  qt_TexCoord0 = qt_MultiTexCoord0;
              }"
</code></pre>
<p>片段着色器这里处理一下，适配出一个符合shaderToy的mainImage作为入口函数</p>
<pre><code class="lang-qml">    readonly property string startCode: "
        void main(void)
        {
            mainImage(gl_FragColor, vec2(vertex.x, iResolution.y - vertex.y));
        }"
    readonly property string defaultPixelShader: "
        void mainImage(out vec4 fragColor, in vec2 fragCoord)
        {
            fragColor = vec4(fragCoord, fragCoord.x, fragCoord.y);
        }"
    property string pixelShader: ""
    fragmentShader: forwardString + (pixelShader ? pixelShader : defaultPixelShader) + startCode
</code></pre>
<p>稍微说明一下，qyvlik大佬的Shader使用gl_FragCoord作为片段坐标传进去了，这种用法的ShaderToy坐标将会占据整个Qml的窗口，</p>
<p>而实际ShaderToy坐标不是整个窗口的时候，超出去的地方就会被切掉，显示出来的只有一小部分。</p>
<p>涛哥研究了一番后，顶点着色器把vertex传过来，vertex.x就是x坐标，vertex.y坐标从上到下是0 - height，而gl_FragCoord 从下到上是0 - height，</p>
<p>所以要翻一下。</p>
<h2>TaoShaderToy</h2>
<p>最后，看一下代码的全貌吧</p>
<pre><code class="lang-qml">//TaoShaderToy.qml
import QtQuick 2.12
import QtQuick.Controls 2.12
/*
vec3        iResolution             image/buffer        The viewport resolution (z is pixel aspect ratio, usually 1.0)
float       iTime                   image/sound/buffer  Current time in seconds
float       iTimeDelta              image/buffer        Time it takes to render a frame, in seconds
int         iFrame                  image/buffer        Current frame
float       iFrameRate              image/buffer        Number of frames rendered per second
float       iChannelTime[4]         image/buffer        Time for channel (if video or sound), in seconds
vec3        iChannelResolution[4]   image/buffer/sound  Input texture resolution for each channel
vec4        iMouse                  image/buffer        xy = current pixel coords (if LMB is down). zw = click pixel
sampler2D   iChannel{i}             image/buffer/sound  Sampler for input textures i
vec4        iDate                   image/buffer/sound  Year, month, day, time in seconds in .xyzw
float       iSampleRate             image/buffer/sound  The sound sample rate (typically 44100)
*/
ShaderEffect {
    id: shader

    //properties for shader

    //not pass to shader
    readonly property vector3d defaultResolution: Qt.vector3d(shader.width, shader.height, shader.width / shader.height)
    function calcResolution(channel) {
        if (channel) {
            return Qt.vector3d(channel.width, channel.height, channel.width / channel.height);
        } else {
            return defaultResolution;
        }
    }
    //pass
    readonly property vector3d  iResolution: defaultResolution
    property real       iTime: 0
    property real       iTimeDelta: 100
    property int        iFrame: 10
    property real       iFrameRate
    property vector4d   iMouse;
    property var iChannel0; //only Image or ShaderEffectSource
    property var iChannel1; //only Image or ShaderEffectSource
    property var iChannel2; //only Image or ShaderEffectSource
    property var iChannel3; //only Image or ShaderEffectSource
    property var        iChannelTime: [0, 1, 2, 3]
    property var        iChannelResolution: [calcResolution(iChannel0), calcResolution(iChannel1), calcResolution(iChannel2), calcResolution(iChannel3)]
    property vector4d   iDate;
    property real       iSampleRate: 44100

    //properties for Qml controller
    property alias hoverEnabled: mouse.hoverEnabled
    property bool running: true
    function restart() {
        shader.iTime = 0
        running = true
        timer1.restart()
    }
    Timer {
        id: timer1
        running: shader.running
        triggeredOnStart: true
        interval: 16
        repeat: true
        onTriggered: {
            shader.iTime += 0.016;
        }
    }
    Timer {
        running: shader.running
        interval: 1000
        onTriggered: {
            var date = new Date();
            shader.iDate.x = date.getFullYear();
            shader.iDate.y = date.getMonth();
            shader.iDate.z = date.getDay();
            shader.iDate.w = date.getSeconds()
        }
    }
    MouseArea {
        id: mouse
        anchors.fill: parent
        onPositionChanged: {
            shader.iMouse.x = mouseX
            shader.iMouse.y = mouseY
        }
        onClicked: {
            shader.iMouse.z = mouseX
            shader.iMouse.w = mouseY
        }
    }
    // 如果环境是OpenGL ES2，默认的version是 version 110, 不需要写出来。
    // 比ES2更老的版本是ES 1.0 和 ES 1.1, 这种古董设备，还是不要玩Shader了吧。
    // ES2没有texture函数，要用旧的texture2D代替
    // 精度限定要写成float
    readonly property string gles2Ver: "
#define texture texture2D
precision mediump float;
"
    // 如果环境是OpenGL ES3，version是 version 300 es
    // ES 3.1 ES 3.2也可以。
    // ES3可以用in out 关键字，gl_FragColor也可以用out fragColor取代
    // 精度限定要写成float
    readonly property string gles3Ver: "#version 300 es
#define varying in
#define gl_FragColor fragColor
precision mediump float;

out vec4 fragColor;
"
    // 如果环境是OpenGL Desktop 3.x，version这里参考Qt默认的version 150。大部分Desktop设备应该都是150
    // 150 即3.2版本，第一个区分Core和Compatibility的版本。Core是核心模式，只有核心api以减轻负担。相应的Compatibility是兼容模式，保留全部API以兼容低版本。
    // 可以用in out 关键字，gl_FragColor也可以用out fragColor取代
    // 精度限定抹掉，用默认的。不抹掉有些情况下会报错，不能通用。
    readonly property string gl3Ver: "#version 150
#define varying in
#define gl_FragColor fragColor
#define lowp
#define mediump
#define highp

out vec4 fragColor;
"
    // 如果环境是OpenGL Desktop 2.x，version这里就用2.0的version 110，即2.0版本
    // 2.x 没有texture函数，要用旧的texture2D代替
    readonly property string gl2Ver: "#version 110
#define texture texture2D
"

    property string versionString: {
        if (Qt.platform.os === "android") {
            if (GraphicsInfo.majorVersion === 3) {
                console.log("android gles 3")
                return gles3Ver
            } else {
                console.log("android gles 2")
                return gles2Ver
            }
        } else {
            if (GraphicsInfo.majorVersion === 3 ||GraphicsInfo.majorVersion === 4) {
                return gl3Ver
            } else {
                return gl2Ver
            }
        }
    }

    vertexShader: "
              uniform mat4 qt_Matrix;
              attribute vec4 qt_Vertex;
              attribute vec2 qt_MultiTexCoord0;
              varying vec2 qt_TexCoord0;
              varying vec4 vertex;
              void main() {
                  vertex = qt_Vertex;
                  gl_Position = qt_Matrix * vertex;
                  qt_TexCoord0 = qt_MultiTexCoord0;
              }"
    readonly property string forwardString: versionString + "
        varying vec2 qt_TexCoord0;
        varying vec4 vertex;
        uniform lowp   float qt_Opacity;

        uniform vec3   iResolution;
        uniform float  iTime;
        uniform float  iTimeDelta;
        uniform int    iFrame;
        uniform float  iFrameRate;
        uniform float  iChannelTime[4];
        uniform vec3   iChannelResolution[4];
        uniform vec4   iMouse;
        uniform vec4    iDate;
        uniform float   iSampleRate;
        uniform sampler2D   iChannel0;
        uniform sampler2D   iChannel1;
        uniform sampler2D   iChannel2;
        uniform sampler2D   iChannel3;
    "
    readonly property string startCode: "
        void main(void)
        {
            mainImage(gl_FragColor, vec2(vertex.x, iResolution.y - vertex.y));
        }"
    readonly property string defaultPixelShader: "
        void mainImage(out vec4 fragColor, in vec2 fragCoord)
        {
            fragColor = vec4(fragCoord, fragCoord.x, fragCoord.y);
        }"
    property string pixelShader: ""
    fragmentShader: forwardString + (pixelShader ? pixelShader : defaultPixelShader) + startCode
}
</code></pre>
