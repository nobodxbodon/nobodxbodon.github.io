<h2>[仓库源文](https://github.com/program-in-chinese/team_website/blob/master/_posts/2018-01-21-Xtext试用: 5步实现一个(中文)JVM语言.markdown)，<a href="http://codeinchinese.com/2018/01/21/Xtext%E8%AF%95%E7%94%A8:-5%E6%AD%A5%E5%AE%9E%E7%8E%B0%E4%B8%80%E4%B8%AA(%E4%B8%AD%E6%96%87">站点原文</a>JVM语言.markdown)</h2>
<hr/>
<h2>layout: post
comments: true
title:  "Xtext试用: 5步实现一个(中文)JVM语言"
description: 利用Xtext实现中文语法的JVM语言. Use Xtext to JVM programming language with Chinese grammar.
date:   2018-01-19 00:00:00 -0700
categories: 语言设计 DSL</h2>
<p>续上文<a href="https://zhuanlan.zhihu.com/p/33047136">Xtext试用: 快速实现简单领域专用语言(DSL)</a>. 基于官方教程: <a href="http://www.eclipse.org/Xtext/documentation/104_jvmdomainmodel.html">Five simple steps to your JVM language</a></p>
<p>达成如下语言:</p>
<p>![2018-01-20-xtext_quan6]({{ "/assets/2018-01-20-xtext_quan6.png" | absolute_url }})</p>
<p>它被Quan6JvmModelInferrer中的转换生成Java源代码(与上文中的代码生成不同的是, 不需要写出具体Java语法).</p>
<p>项目源码在: <a href="https://github.com/program-in-chinese/xtext_tutorial_15_min_zh/tree/master/com.program_in_chinese.quan6">program-in-chinese/xtext_tutorial_15_min_zh</a>, 只包含了语法规则所在项目. 其他项目(.ide, .tests, .ui.tests等等, 也不知何用)未提交. 测试项目(包括上图的代码)在: <a href="https://github.com/program-in-chinese/xtext_tutorial_15_min_zh/tree/master/testQuan6">program-in-chinese/xtext_tutorial_15_min_zh</a></p>
<p>由于本人创建的项目名称与教程不同(com.program_in_chinese.quan6), 运行后打开DSL源码文件时报错:</p>
<pre><code>An internal error occurred during: "XtextReconcilerJob".
java.lang.IllegalArgumentException: Domainmodel.importSection does not exist
</code></pre>
<p>后发现语法规则的命名空间应该修改为自定义的(本文用的Quan6, 而非教程中的Domainmodel). 顺便将关键词汉化了, 标识符支持中文(IDENTIFIER等), 更新后的语法规则如下:</p>
<pre><code class="language-bnf">grammar com.program_in_chinese.quan6.Quan6 with org.eclipse.xtext.xbase.Xbase

generate quan6 "http://www.program_in_chinese.com/quan6/Quan6"

Quan6:
	importSection=XImportSection?
    elements+=AbstractElement*;
 
AbstractElement:
    PackageDeclaration | Entity;
 
PackageDeclaration:
    '包' name=QualifiedName '{'
        elements+=AbstractElement*
    '}';
 
Entity:
    '类' name=ValidID ('扩展' superType=JvmTypeReference)? '{'
        features+=Feature*
    '}';
 
Feature:
    Property | Operation;
 
Property:
    name=ValidID ':' type=JvmTypeReference;
 
Operation:
    '函数' name=ValidID 
        '('(params+=FullJvmFormalParameter 
        (',' params+=FullJvmFormalParameter)*)?')'
        ':' type=JvmTypeReference 
    body=XBlockExpression;

@Override
ValidID:
	IDENTIFIER;
	
terminal IDENTIFIER:
	'^'?('\u4E00'..'\u9FA5'|'\uF900'..'\uFA2D'|'a'..'z'|'A'..'Z'|'_') ('a'..'z'|'A'..'Z'|'_'|'0'..'9'|'\u4E00'..'\u9FA5'|'\uF900'..'\uFA2D')*;
</code></pre>
<p>其中不少xbase支持的规则如XBlockExpression, 就是Java块, 如:</p>
<pre><code>{
    return "Hello World" + "!"
}
</code></pre>
<h4>小结</h4>
<p>这个教程中演示的语法定制程度似乎有限, 在深入学习xtext语法规则描述语言(<a href="http://www.eclipse.org/Xtext/documentation/301_grammarlanguage.html">The Grammar Language</a>)和xbase之前, 还不能确认能够支持多么自由的语法定义. 但暂时没想到什么肯定能不到的语法规则(未把不带空格语法考虑在内). 感觉工具的一些错误反馈信息不那么易懂, 使用过程中感觉不那么结实. 但至少应该可以用于语言原型的快速开发.</p>
