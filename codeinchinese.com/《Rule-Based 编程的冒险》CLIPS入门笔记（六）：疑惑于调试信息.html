<h2><a href="https://github.com/program-in-chinese/team_website/blob/master/_posts/2025-03-17-《Rule-Based 编程的冒险》CLIPS入门笔记（六）：疑惑于调试信息.md">仓库源文</a>，<a href="http://codeinchinese.com/2025/03/17/《Rule-Based-编程的冒险》CLIPS入门笔记（六）：疑惑于调试信息">站点原文</a></h2>
<p>接<a href="https://zhuanlan.zhihu.com/p/30539558081">上文</a>，继续第四章。另外，随着规则逐渐复杂，不知如何自动批量单元测试？</p>
<h2>场景描述</h2>
<p>类似对地点规则的事实化。</p>
<pre><code>(deffacts 场景
   (物 (id 蘑菇)
          (位置 地牢北面)
          (种类 场景)
          (描述 "它已被压烂，我"
               "可别再摔一次。"))
   (物 (id 遗体)
          (位置 地牢北面)
          (种类 场景)
          (描述 "不用说，这些就是"
               "没被蘑菇接住的后果。"))
   (物 (id 废墟)
          (位置 地牢南面)
          (种类 场景)))
</code></pre>
<h2>添加描述所见规则</h2>
<p>模式匹配的部分很有点 sql 的感觉，类似 <code>select * from 物 where 物.位置 = 冒险家.位置 and 物.描述is not empty</code>。</p>
<pre><code>(defrule 描述所见

   ;; 命令为 看 &lt;物&gt;

   ?令 &lt;- (命令 (行动 看 ?id))

   ;; 获取冒险家所在位置

   (物 (id 冒险家)
       (位置 ?位置))

   ;; 【匹配条件三】该 id 对应物在冒险家所处位置 
   ;; 或者由冒险家拿着（这第二个条件之前没有）

   (物 (id ?id)
       (位置 ?位置 | 冒险家)
       (描述 $?文本))

   ;; 确认描述文本长度大于0

   (test (&gt; (length$ ?文本) 0))

   =&gt;

   ;; 照例删除已执行命令

   (retract ?令)

   ;; 逐行显示描述

   (foreach ?行 ?文本
      (println ?行)))
</code></pre>
<h2>看无关紧要的物件</h2>
<p>此规则与上面类似，【匹配条件三】改为描述为空：</p>
<pre><code>   (物 (id ?id)
       (位置 ?位置 | 冒险家)
       (描述))
</code></pre>
<h2>看不到</h2>
<p>【匹配条件三】改为：</p>
<ul>
<li>不存在该 id 或者 </li>
<li>该物不在冒险家所在位置 或者</li>
<li>该物不被冒险家拿着</li>
</ul>
<pre><code>(not (物 (id ?id)
         (位置 ?位置 | 冒险家)))
</code></pre>
<p>匹配条件的可读性随着复杂度增加而增加，尤其是特殊符号和Lisp风格的括号匹配很需要适应。</p>
<h2>其他 predicate function</h2>
<p>test 后除了 &gt; 外还支持很多其他判断如：</p>
<pre><code>CLIPS&gt; (= 3 3.0)
TRUE
CLIPS&gt; (&lt;&gt; 3 4)
TRUE
CLIPS&gt; (eq 3 3.0)
FALSE
CLIPS&gt; (floatp 3)
FALSE
</code></pre>
<h2>设置断点</h2>
<p>在触发对应规则前暂停：</p>
<pre><code>CLIPS&gt; (set-break 所见无关紧要)
CLIPS&gt; (set-break 描述所见)
CLIPS&gt; (set-break 看不到)
CLIPS&gt; (show-breaks)
描述所见
所见无关紧要
看不到
CLIPS&gt; (run)
被小妖抓住后，你被
丢进了巢穴的地牢里。

你在地牢北角。
有个巨型蘑菇。
地上横七竖八，
都是探险者的尸首。

&gt; 看 蘑菇
Breaking on rule 描述所见.
</code></pre>
<h2>规则匹配情况</h2>
<pre><code>CLIPS&gt; (matches 描述所见)
Matches for Pattern 1
f-8
Matches for Pattern 2
f-2
Matches for Pattern 3
f-2
f-3
f-4
f-5
f-6
f-7
Partial matches for CEs 1 - 2
f-8,f-2
Partial matches for CEs 1 - 3
f-8,f-2,f-5
Activations
f-8,f-2,f-5
(8 2 1)
</code></pre>
<p>(8 2 1) 意思是，有 8 个匹配（1+1+6），2个部分匹配，一个触发。</p>
<p>这里有个疑问，为何第四个匹配条件 <code>(test (&gt; (length$ ?文本) 0))</code> 没有列为 Pattern 4？按照如下事实：</p>
<pre><code>CLIPS&gt; (ppfact 2)
(物 
   (id 冒险家) 
   (种类 角色) 
   (位置 地牢北面) 
   (描述))
CLIPS&gt; (ppfact 3)
(物 
   (id 地牢北面) 
   (种类 地点) 
   (位置 虚空) 
   (描述 "你在地牢北角。" "有个巨型蘑菇。" "地上横七竖八，" "都是探险者的尸首。"))
CLIPS&gt; (ppfact 4)
(物 
   (id 地牢南面) 
   (种类 地点) 
   (位置 虚空) 
   (描述 "你在地牢南角。" "一大堆瓦砾从" "墙壁上垮了下来。"))
CLIPS&gt; (ppfact 5)
(物 
   (id 蘑菇) 
   (种类 场景) 
   (位置 地牢北面) 
   (描述 "它已被压烂，我" "可别再摔一次。"))
CLIPS&gt; (ppfact 6)
(物 
   (id 遗体) 
   (种类 场景) 
   (位置 地牢北面) 
   (描述 "不用说，这些就是" "没被蘑菇接住的后果。"))
CLIPS&gt; (ppfact 7)
(物 
   (id 废墟) 
   (种类 场景) 
   (位置 地牢南面) 
   (描述))
</code></pre>
<p>照理说调试信息里还应有此项？</p>
<pre><code>Matches for Pattern 4
f-3
f-4
f-5
f-6
</code></pre>
<h2>相关事实检查</h2>
<p>上面调试信息里用 f-x 指代各事实有点不方便调试，但如果展开估计又太过杂乱。</p>
<pre><code>CLIPS&gt; (ppfact 5)
(物 
   (id 蘑菇) 
   (种类 场景) 
   (位置 地牢北面) 
   (描述 "它已被压烂，我" "可别再摔一次。"))
CLIPS&gt; (ppfact 8)
(命令 
   (行动 看 蘑菇))
CLIPS&gt; (ppfact 2)
(物 
   (id 冒险家) 
   (种类 角色) 
   (位置 地牢北面) 
   (描述))
</code></pre>
<h2>继续运行</h2>
<pre><code>CLIPS&gt; (run)
它已被压烂，我
可别再摔一次。
</code></pre>
<h2>看不到规则</h2>
<pre><code>&gt; 看 精灵
Breaking on rule 看不到.
CLIPS&gt; (matches 看不到)
Matches for Pattern 1
f-9
Matches for Pattern 2
f-2
Matches for Pattern 3
f-2
f-3
f-4
f-5
f-6
f-7
Partial matches for CEs 1 - 2
f-9,f-2
Partial matches for CEs 1 - 3
f-9,f-2,*
Activations
f-9,f-2,*
(8 2 1)
</code></pre>
<p>这里的第三个条件为何为*，书中解释：</p>
<blockquote><p>there are several facts matching the third pattern, but there isn't a thing fact with an id slot value of 精灵, so the 看不到 rule is activated by the absense of that fact. As is done for a rule with no conditions, the absence of a fact is shown in an activation using the * charactor</p>
</blockquote>
<h2>删除断点</h2>
<pre><code>CLIPS&gt; (show-breaks)
描述所见
所见无关紧要
看不到
CLIPS&gt; (remove-break 描述所见)
CLIPS&gt; (show-breaks)
所见无关紧要
看不到
CLIPS&gt; (remove-break)
CLIPS&gt; (show-breaks)
CLIPS&gt;
</code></pre>
<h2>常见报错</h2>
<pre><code>CLIPS&gt; (floatq 3)

[EXPRNPSR3] Missing function declaration for 'floatq'.
</code></pre>
<p>【调试信息里的 test 对应 pattern，以及 not 条件的含义，还没想清楚，也许需回到第一章作些试验】</p>
