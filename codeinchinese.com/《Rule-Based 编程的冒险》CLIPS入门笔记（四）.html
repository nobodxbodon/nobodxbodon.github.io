<h2><a href="https://github.com/program-in-chinese/team_website/blob/master/_posts/2025-03-14-《Rule-Based 编程的冒险》CLIPS入门笔记（四）.md">仓库源文</a>，<a href="http://codeinchinese.com/2025/03/14/《Rule-Based-编程的冒险》CLIPS入门笔记（四）">站点原文</a></h2>
<p>接 <a href="https://zhuanlan.zhihu.com/p/30128666727">上文</a>。</p>
<h2>处理所有无法识别的命令</h2>
<p>新添低优先级规则。</p>
<pre><code>CLIPS&gt; (watch facts)
CLIPS&gt; (watch activations)
CLIPS&gt; (assert (命令 (行动 向南 走)))
==&gt; f-2     (命令 (行动 向南 走))
==&gt; Activation 0      由北往南: f-1,f-2
&lt;Fact-2&gt;
CLIPS&gt; (defrule 不明命令 
   (declare (salience -10))
   ?令 &lt;- (命令)
   =&gt;
   (println "不理解命令。")
   (retract ?令))
==&gt; Activation -10    不明命令: f-2
CLIPS&gt; (agenda)
0      由北往南: f-1,f-2
-10    不明命令: f-2
For a total of 2 activations.
</code></pre>
<h2>运行可识别命令</h2>
<p>f-2 删除时，<code>不明命令</code> 规则由于条件缺失而失活。</p>
<pre><code>CLIPS&gt; (watch rules)
CLIPS&gt; (run)
FIRE    1 由北往南: f-1,f-2
&lt;== f-2     (命令 (行动 向南 走))
&lt;== Activation -10    不明命令: f-2
&lt;== f-1     (物 ... (位置 地牢北面))
==&gt; f-1     (物 ... (位置 地牢南面))
==&gt; Activation 0      地牢南面: f-1
FIRE    2 地牢南面: f-1
你在地牢南角。
一大堆瓦砾从
墙壁上垮了下来。
</code></pre>
<h2>运行不可识别命令</h2>
<pre><code>CLIPS&gt; (assert (命令 (行动 攻击 小妖)))
==&gt; f-3     (命令 (行动 攻击 小妖))
==&gt; Activation -10    不明命令: f-3
&lt;Fact-3&gt;
CLIPS&gt; (agenda)
-10    不明命令: f-3
For a total of 1 activation.
CLIPS&gt; (run)
FIRE    1 不明命令: f-3
不理解命令。
&lt;== f-3     (命令 (行动 攻击 小妖))
</code></pre>
<h2>读取用户输入生成对应命令</h2>
<p>通过读入一行指令、小写规整、字符串转为符号后，再将符号列表转换为命令事实。</p>
<pre><code>CLIPS&gt; (defrule 获取命令
   (declare (salience -10))
   (not (命令))
   =&gt;
   (println)
   (print "&gt; ")
   (bind ?用户输入 (explode$ (lowcase (readline))))
   (assert (命令 (行动 ?用户输入))))
==&gt; Activation -10    获取命令: *
CLIPS&gt; (agenda)
-10    获取命令: *
For a total of 1 activation.
CLIPS&gt; (matches 获取命令)
Matches for Pattern 1
 None
Activations
*
(0 0 1)
</code></pre>
<h2>测试该规则</h2>
<p>指定最多执行一条规则。用户输入后可见新添命令事实 f-4，触发 <code>不明命令</code> 规则。</p>
<pre><code>CLIPS&gt; (run 1)
FIRE    1 获取命令: *

&gt; 攻击 小妖
==&gt; f-4     (命令 (行动 攻击 小妖))
==&gt; Activation -10    不明命令: f-4
</code></pre>
<h2>规则的匹配情况</h2>
<p>两条条件相反的规则，在命令存在时，仅一条被触发。</p>
<pre><code>CLIPS&gt; (matches 不明命令)
Matches for Pattern 1
f-4
Activations
f-4
(1 0 1)
CLIPS&gt; (matches 获取命令)
Matches for Pattern 1
f-4
Activations
 None
(1 0 0)
</code></pre>
<h2>命令运行后继续获取</h2>
<pre><code>CLIPS&gt; (run 1)
FIRE    1 不明命令: f-4
不理解命令。
&lt;== f-4     (命令 (行动 攻击 小妖))
==&gt; Activation -10    获取命令: *
CLIPS&gt; (agenda)
-10    获取命令: *
For a total of 1 activation.
</code></pre>
<p>如果运行 run，则会进入无尽循环获取命令。</p>
<h2>退出命令</h2>
<p>在 CLIPS IDE 中，万一遇到规则无尽循环，还可用 Halt Rules 中止。于是新添此命令。</p>
<pre><code>CLIPS&gt; (defrule 退出
   ?c &lt;- (命令 (行动 退出))
   =&gt;
   (retract ?c)
   (halt))
</code></pre>
<h2>运行退出</h2>
<p>退出后，已触发的规则仍在议程中，可继续运行。</p>
<pre><code>CLIPS&gt; (run)
FIRE    1 获取命令: *

&gt; 退出
==&gt; f-5     (命令 (行动 退出))
==&gt; Activation 0      退出: f-5
==&gt; Activation -10    不明命令: f-5
FIRE    2 退出: f-5
&lt;== f-5     (命令 (行动 退出))
&lt;== Activation -10    不明命令: f-5
==&gt; Activation -10    获取命令: *
[PRCCODE4] WARNING: Execution halted during the actions of defrule '退出'.
CLIPS&gt; (agenda)
-10    获取命令: *
For a total of 1 activation.
</code></pre>
<h2>完整运行效果</h2>
<p>将新添的规则加入 kernel.clp 重新加载运行如下：</p>
<pre><code>CLIPS&gt; (batch* runq.bat)
被小妖抓住后，你被
丢进了巢穴的地牢里。

你在地牢北角。
有个巨型蘑菇。
地上横七竖八，
都是探险者的尸首。

&gt; 向南 走
你在地牢南角。
一大堆瓦砾从
墙壁上垮了下来。

&gt; 向上 走
墙太滑了。

&gt; 退出
TRUE
</code></pre>
<h2>看东西</h2>
<p>新添规则：</p>
<pre><code>CLIPS&gt; (defrule 看蘑菇
   ?令 &lt;- (命令 (行动 看 蘑菇))
   (物 (id 冒险家)
       (位置 地牢北面))
   =&gt;
   (retract ?令)
   (println "它已被压烂，我")
   (println "可别再掉一次。"))  
CLIPS&gt; (defrule 看遗体
   ?令 &lt;- (命令 (行动 看 遗体))
   (物 (id 冒险家)
       (位置 地牢北面))
   =&gt;
   (retract ?令)
   (println "不用说，这些就是")
   (println "没被它接住的后果。"))
</code></pre>
<h2>运行效果</h2>
<p>续前，继续运行。</p>
<pre><code>CLIPS&gt; (run)

&gt; 向北 走
你在地牢北角。
有个巨型蘑菇。
地上横七竖八，
都是探险者的尸首。

&gt; 看 蘑菇
它已被压烂，我
可别再掉一次。

&gt; 看 遗体
不用说，这些就是
没被它接住的后果。

&gt; 退出
</code></pre>
<h2>看别处</h2>
<p>暂时不识别别处：</p>
<pre><code>CLIPS&gt; (run)

&gt; 看 墙
不理解命令。

&gt; 退出
</code></pre>
<p>新添规则，匹配 <code>看 xxx</code> 格式的命令，优先级高于 <code>不明命令</code>。测试：</p>
<pre><code>CLIPS&gt; (defrule 看其他
   (declare (salience -5))
   ?令 &lt;- (命令 (行动 看 ?))
   =&gt;
   (retract ?令)
   (println "没什么特别的。"))
CLIPS&gt; (run)

&gt; 看 墙
没什么特别的。

&gt; 退出
</code></pre>
<p>【第三章完】</p>
